apply plugin: 'com.android.application'

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "com.example.wyyu.gitsamlpe"
    minSdkVersion 16
    targetSdkVersion 28
    multiDexEnabled true
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    externalNativeBuild {
      cmake {
        cppFlags ""
      }
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }
  buildToolsVersion '28.0.3'

  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  //noinspection GradleCompatible
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation 'com.google.android.material:material:1.1.0'
  // 分包，解决 64K 限制
  implementation 'androidx.multidex:multidex:2.0.1'
  // Fresco，图片加载框架
  implementation 'com.facebook.fresco:fresco:2.1.0'
  implementation 'com.facebook.fresco:animated-gif:2.0.0'
  implementation 'com.facebook.fresco:animated-webp:2.0.0'
  implementation 'com.facebook.fresco:webpsupport:2.0.0'
  // ButterKnife，注解绑定控件框架
  implementation 'com.jakewharton:butterknife:10.2.1'
  annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
  // RecyclerView
  implementation 'androidx.recyclerview:recyclerview:1.1.0'
  // 高德地图 API
  implementation 'com.amap.api:location:3.7.0'
  // RXJava
  implementation 'io.reactivex:rxandroid:1.2.1'
  implementation 'io.reactivex:rxjava:1.3.8'
  // Retrofit + OKHttp，网络请求框架
  implementation 'com.squareup.retrofit2:retrofit:2.7.1'
  implementation 'com.squareup.okhttp3:okhttp:4.3.0'
  // FastJson，非常快的 Json 解析框架
  implementation 'com.alibaba:fastjson:1.2.62'
  // 自动缩放 TextView
  implementation 'com.ms-square:expandableTextView:0.1.4'
  // 卡片式 View
  implementation 'androidx.cardview:cardview:1.0.0'
  // 本地文件读写框架
  implementation 'commons-io:commons-io:2.6'
  // JZ 视频播放器
  implementation 'cn.jzvd:jiaozivideoplayer:7.1.0'
  // EXO 视频播放器
  implementation 'com.google.android.exoplayer:exoplayer:2.11.1'
  implementation 'com.google.android.exoplayer:extension-okhttp:2.10.8'
  implementation 'com.android.installreferrer:installreferrer:2.1'
  // 本地图片选择框架
  implementation 'com.zhihu.android:matisse:0.4.3'
  // 图片编辑框架
  implementation 'com.github.yalantis:ucrop:2.2.2-native'
  // 官方MVP架构 LifeCycle
  implementation 'androidx.lifecycle:lifecycle-runtime:2.2.0'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
  // 可切换软键盘
  implementation 'cn.dreamtobe.kpswitch:library:1.6.1'
  // 数据库读写框架
  implementation 'com.tencent.wcdb:wcdb-android:1.0.8'
  implementation 'androidx.sqlite:sqlite:2.1.0'
  // 消息收发框架
  implementation 'com.jeremyliao:live-event-bus:1.1.0'
  // 可缩放查看大图库
  implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
  // SpringAnimation
  implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
  // Tangram 动态页面框架
  implementation 'com.alibaba.android:tangram:2.2.5'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
  // Lottie 动画加载框架
  implementation 'com.airbnb.android:lottie:3.1.0'
  // OKDownload 文件下载框架
  implementation 'com.liulishuo.okdownload:okdownload:1.0.5'
  implementation 'com.liulishuo.okdownload:sqlite:1.0.5'
  implementation 'com.liulishuo.okdownload:okhttp:1.0.5'
  // Litho 动态加载布局框架
  implementation 'com.facebook.litho:litho-core:0.32.0'
  implementation 'com.facebook.litho:litho-widget:0.32.0'
  implementation 'com.facebook.litho:litho-annotations:0.32.0'
  annotationProcessor 'com.facebook.litho:litho-processor:0.21.0'
  // Litho 通过 Fresco 加载图片资源
  implementation 'com.facebook.litho:litho-fresco:0.32.0'
  // aspectj库
  implementation group: 'org.aspectj', name: 'aspectjrt', version: '1.9.4'
  // svga动画播放框架
  implementation 'com.github.yyued:SVGAPlayer-Android:2.5.2'
  // 下拉刷新控件
  implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-14'
  implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-alpha-14'

  testImplementation 'junit:junit:4.13'
  androidTestImplementation 'androidx.test:runner:1.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

  implementation project(':matisse')
  implementation project(':banner')
  implementation project(':multi')

  implementation project(':debug_annotate')
  annotationProcessor project(':debug_processor')
  implementation 'org.jetbrains:annotations:15.0'
}

/*Aspectj配置*/
import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

//log 打印工具和构建配置
final def log = project.logger
//这个地方必须在 apply plugin: 'com.android.application' 的 build.gradle 中执行，否则报错
final def variants = project.android.applicationVariants

variants.all { variant ->
  // 通过debug判断是否需要打入aspectj，如果release不需要打则会return掉
  if (!variant.buildType.isDebuggable()) {
    log.debug("Skipping non-debuggable build type '${variant.buildType.name}'.")
    return;
  }

  //使 aspectj 配置生效
  JavaCompile javaCompile = variant.javaCompile
  javaCompile.doLast {
    String[] args = ["-showWeaveInfo",
                     "-1.8",
                     "-inpath", javaCompile.destinationDir.toString(),
                     "-aspectpath", javaCompile.classpath.asPath,
                     "-d", javaCompile.destinationDir.toString(),
                     "-classpath", javaCompile.classpath.asPath,
                     "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
    log.debug "ajc args: " + Arrays.toString(args)

    MessageHandler handler = new MessageHandler(true);
    new Main().run(args, handler);
    //在编译时打印信息如警告、error 等等
    for (IMessage message : handler.getMessages(null, true)) {
      switch (message.getKind()) {
        case IMessage.ABORT:
        case IMessage.ERROR:
        case IMessage.FAIL:
          log.error message.message, message.thrown
          break;
        case IMessage.WARNING:
          log.warn message.message, message.thrown
          break;
        case IMessage.INFO:
          log.info message.message, message.thrown
          break;
        case IMessage.DEBUG:
          log.debug message.message, message.thrown
          break;
      }
    }
  }
}
