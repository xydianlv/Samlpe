apply plugin: 'com.android.application'

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "com.example.wyyu.gitsamlpe"
    minSdkVersion 16
    targetSdkVersion 28
    multiDexEnabled true
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    externalNativeBuild {
      cmake {
        cppFlags ""
      }
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }
  buildToolsVersion '26.0.3'
  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  //noinspection GradleCompatible
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.android.support.constraint:constraint-layout:1.1.3'
  implementation 'com.android.support:design:28.0.0'
  // 分包，解决 64K 限制
  implementation 'com.android.support:multidex:1.0.3'
  // Fresco，图片加载框架
  implementation 'com.facebook.fresco:fresco:1.13.0'
  implementation 'com.facebook.fresco:animated-gif:1.13.0'
  implementation 'com.facebook.fresco:animated-webp:1.13.0'
  implementation 'com.facebook.fresco:webpsupport:1.13.0'
  // ButterKnife，注解绑定控件框架
  //noinspection GradleDependency
  implementation 'com.jakewharton:butterknife:8.8.1'
  //noinspection GradleDependency
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
  // RecyclerView
  implementation 'com.android.support:recyclerview-v7:28.0.0'
  // 高德地图 API
  implementation 'com.amap.api:location:3.7.0'
  // RXJava
  implementation 'io.reactivex:rxandroid:1.2.1'
  implementation 'io.reactivex:rxjava:1.3.8'
  // Retrofit + OKHttp，网络请求框架
  implementation 'com.squareup.retrofit2:retrofit:2.5.0'
  //noinspection GradleDependency
  implementation 'com.squareup.okhttp3:okhttp:3.11.0'
  // FastJson，非常快的 Json 解析框架
  implementation 'com.alibaba:fastjson:1.2.46'
  // 自动缩放 TextView
  implementation 'com.ms-square:expandableTextView:0.1.4'
  // 卡片式 View
  implementation 'com.android.support:cardview-v7:28.0.0'
  // 本地文件读写框架
  implementation 'commons-io:commons-io:2.6'
  // JZ 视频播放器
  implementation 'cn.jzvd:jiaozivideoplayer:6.2.12'
  // EXO 视频播放器
  //noinspection GradleDependency
  implementation 'com.google.android.exoplayer:exoplayer:2.8.4'
  // 本地图片选择框架
  implementation 'com.zhihu.android:matisse:0.4.3'
  // 图片编辑框架
  implementation 'com.github.yalantis:ucrop:2.2.2-native'
  // 官方MVP架构 LifeCycle
  implementation "android.arch.lifecycle:runtime:1.1.1"
  implementation 'android.arch.lifecycle:extensions:1.1.1'
  // 可切换软键盘
  implementation 'cn.dreamtobe.kpswitch:library:1.6.1'
  // 数据库读写框架
  implementation 'com.tencent.wcdb:wcdb-android:1.0.8'
  implementation 'android.arch.persistence:db:1.1.1'
  // 消息收发框架
  implementation 'com.jeremyliao:live-event-bus:1.1.0'
  // 可缩放查看大图库
  implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
  // SpringAnimation
  implementation 'com.android.support:support-dynamic-animation:28.0.0'
  // Tangram 动态页面框架
  implementation 'com.alibaba.android:tangram:2.2.5'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
  // Lottie 动画加载框架
  implementation 'com.airbnb.android:lottie:2.7.0'
  // OKDownload 文件下载框架
  implementation 'com.liulishuo.okdownload:okdownload:1.0.5'
  implementation 'com.liulishuo.okdownload:sqlite:1.0.5'
  implementation 'com.liulishuo.okdownload:okhttp:1.0.5'
  // Litho 动态加载布局框架
  //noinspection GradleDependency
  implementation 'com.facebook.litho:litho-core:0.21.0'
  //noinspection GradleDependency
  implementation 'com.facebook.litho:litho-widget:0.21.0'
  //noinspection GradleDependency
  implementation 'com.facebook.litho:litho-annotations:0.21.0'
  //noinspection GradleDependency
  annotationProcessor 'com.facebook.litho:litho-processor:0.21.0'
  // Litho 框架已扩展的自定义控件
  // noinspection GradleDependency
  implementation 'com.facebook.litho:litho-sections-core:0.21.0'
  // noinspection GradleDependency
  implementation 'com.facebook.litho:litho-sections-widget:0.21.0'
  // noinspection GradleDependency
  implementation 'com.facebook.litho:litho-sections-annotations:0.21.0'
  // Litho 通过 Fresco 加载图片资源
  // noinspection GradleDependency
  implementation 'com.facebook.litho:litho-fresco:0.21.0'
  // aspectj库
  implementation group: 'org.aspectj', name: 'aspectjrt', version: '1.9.4'
  // svga动画播放框架
  implementation 'com.github.yyued:SVGAPlayer-Android:2.5.2'

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

  implementation project(':matisse')
  implementation project(':banner')
  implementation project(':multi')

  implementation project(':debug_annotate')
  annotationProcessor project(':debug_processor')
  implementation 'org.jetbrains:annotations:15.0'
}

/*Aspectj配置*/
import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

//log 打印工具和构建配置
final def log = project.logger
//这个地方必须在 apply plugin: 'com.android.application' 的 build.gradle 中执行，否则报错
final def variants = project.android.applicationVariants

variants.all { variant ->
  // 通过debug判断是否需要打入aspectj，如果release不需要打则会return掉
  if (!variant.buildType.isDebuggable()) {
    log.debug("Skipping non-debuggable build type '${variant.buildType.name}'.")
    return;
  }

  //使 aspectj 配置生效
  JavaCompile javaCompile = variant.javaCompile
  javaCompile.doLast {
    String[] args = ["-showWeaveInfo",
                     "-1.8",
                     "-inpath", javaCompile.destinationDir.toString(),
                     "-aspectpath", javaCompile.classpath.asPath,
                     "-d", javaCompile.destinationDir.toString(),
                     "-classpath", javaCompile.classpath.asPath,
                     "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
    log.debug "ajc args: " + Arrays.toString(args)

    MessageHandler handler = new MessageHandler(true);
    new Main().run(args, handler);
    //在编译时打印信息如警告、error 等等
    for (IMessage message : handler.getMessages(null, true)) {
      switch (message.getKind()) {
        case IMessage.ABORT:
        case IMessage.ERROR:
        case IMessage.FAIL:
          log.error message.message, message.thrown
          break;
        case IMessage.WARNING:
          log.warn message.message, message.thrown
          break;
        case IMessage.INFO:
          log.info message.message, message.thrown
          break;
        case IMessage.DEBUG:
          log.debug message.message, message.thrown
          break;
      }
    }
  }
}
